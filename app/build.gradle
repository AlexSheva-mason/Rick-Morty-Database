apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
// Firebase Performance Monitoring plugin
apply plugin: 'com.google.firebase.firebase-perf'
// Firebase Crashlytics Gradle plugin
apply plugin: 'com.google.firebase.crashlytics'

ext.versionMajor = 1
ext.versionMinor = 1
ext.versionPatch = 8
ext.versionClassifier = null
ext.minimumSdkVersion = 21

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    bundle {
        language {
            enableSplit = false
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    buildFeatures {
        viewBinding = true
    }

    defaultConfig {
        applicationId "com.shevaalex.android.rickmortydatabase"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion 29
        versionCode generateVersionCode()
        versionName generateVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true

            shrinkResources true

            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier != null && !ext.versionClassifier.isEmpty()) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'

    // Google Tag Manager
    implementation 'com.google.android.gms:play-services-tagmanager:17.0.0'

    // Firebase remote config
    implementation 'com.google.firebase:firebase-config:19.2.0'
    // Firebase SDK for Google Analytics
    implementation 'com.google.firebase:firebase-analytics:17.5.0'
    // Firebase performance monitoring
    implementation 'com.google.firebase:firebase-perf:19.0.8'
    // Firebase Crashlytics SDK.
    implementation 'com.google.firebase:firebase-crashlytics:17.2.1'

    // Material Design components
    implementation 'com.google.android.material:material:1.2.0'

    // Testing
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // Recyclerview
    implementation "androidx.recyclerview:recyclerview:1.2.0-alpha05"

    // Volley
    implementation 'com.android.volley:volley:1.1.1'

    // Retrofit, Gson
    def retrofitVersion = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    //RecyclerView Fast scroll
    implementation 'me.zhanghai.android.fastscroll:library:1.1.4'

    // CardView
    implementation "androidx.cardview:cardview:1.0.0"

    // Picasso, OkHTTP for Picasso
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation "com.squareup.okhttp3:okhttp:4.7.2"

    // Lifecycle components dependencies
    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"

    // Room dependencies
    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    // Navigation components
    def nav_version = "2.3.0"
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"
    apply plugin: "androidx.navigation.safeargs"

    // Paging
    def paging_version = "2.1.2"
    implementation "androidx.paging:paging-runtime:$paging_version"

    // Androidx preference library
    implementation "androidx.preference:preference:1.1.1"

    // LeakCanary debugImplementation (should only run in debug builds)
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.3'
}
