apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
// Firebase Performance Monitoring plugin
apply plugin: 'com.google.firebase.firebase-perf'
// Firebase Crashlytics Gradle plugin
apply plugin: 'com.google.firebase.crashlytics'
// Kotlin
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: "kotlin-parcelize"

ext.versionMajor = 2
ext.versionMinor = 0
ext.versionPatch = 3
ext.versionClassifier = ""
ext.minimumSdkVersion = 24

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    bundle {
        language {
            enableSplit = false
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    buildFeatures {
        viewBinding = true
    }

    defaultConfig {
        applicationId "com.shevaalex.android.rickmortydatabase"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion 30
        versionCode generateVersionCode()
        versionName generateVersionName()

        testInstrumentationRunner "com.shevaalex.android.rickmortydatabase.TestRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"]
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true

            shrinkResources true

            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kapt {
        correctErrorTypes true
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }

}

@SuppressWarnings('GrMethodMayBeStatic')
def generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

@SuppressWarnings('GrMethodMayBeStatic')
def generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier != null && !ext.versionClassifier.isEmpty()) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    //versions
    def dagger_version = "2.30.1"

    //Kotlin
    def coroutines_version = "1.3.9"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    //AndroidX
    def androidx_version = "1.5.0-beta01"
    //androidx.core:core non ktx needed here explicitly to remove greylisted API warning Landroid/graphics/FontFamily
    //noinspection KtxExtensionAvailable
    implementation "androidx.core:core:$androidx_version"
    implementation "androidx.core:core-ktx:$androidx_version"


    //BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:26.2.0')
    //Firebase common
    implementation 'com.google.firebase:firebase-common-ktx'
    // Firebase remote config
    implementation 'com.google.firebase:firebase-config-ktx'
    // Firebase SDK for Google Analytics
    implementation 'com.google.firebase:firebase-analytics-ktx'
    // Firebase performance monitoring
    implementation 'com.google.firebase:firebase-perf-ktx'
    // Firebase Crashlytics SDK.
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    //Firebase Authentication library
    implementation 'com.google.firebase:firebase-auth-ktx'
    //Google play api kotlin coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.4.1'

    // Material Design components
    implementation 'com.google.android.material:material:1.2.1'

    // Testing
    // Local Unit Tests
    testImplementation 'junit:junit:4.13'
    testImplementation 'com.google.truth:truth:1.0.1'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.2'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    // Instrumented Unit Tests
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'com.google.truth:truth:1.0.1'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.2'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"

    // Recyclerview
    implementation "androidx.recyclerview:recyclerview:1.2.0-beta01"

    // Retrofit
    def retrofitVersion = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    //Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    //Glide
    def glide_version = "4.11.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // Lifecycle components dependencies
    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // Room dependencies
    def room_version = "2.2.6"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Navigation components
    def nav_version = "2.3.3"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Paging
    def paging_version = "2.1.2"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    // Androidx preference library
    implementation "androidx.preference:preference-ktx:1.1.1"

    // LeakCanary debugImplementation (should only run in debug builds)
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.3'

    //Dagger2
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    //Timber for logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //Material Dialogs by afollestad ***https://github.com/afollestad/material-dialogs***
    def material_afollestad_version = "3.2.1"
    implementation "com.afollestad.material-dialogs:core:$material_afollestad_version"
    implementation "com.afollestad.material-dialogs:lifecycle:$material_afollestad_version"

    //Google Play Core Api
    def google_play_core_version = "1.9.1"
    def google_play_core_ktx_version = "1.8.1"
    implementation "com.google.android.play:core:$google_play_core_version"
    implementation "com.google.android.play:core-ktx:$google_play_core_ktx_version"

}
